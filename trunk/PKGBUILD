# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Maximilian KÃ¶hl <linuxmaxi@googlemail.com>

pkgname=('python-gtkspellcheck' 'python2-gtkspellcheck')
pkgver=4.0.5
pkgrel=1
pkgdesc="Spell-checking library written in Python for Gtk based on Enchant"
arch=('any')
url="https://github.com/koehlma/pygtkspellcheck"
license=('GPL')
makedepends=('gtk3' 'python-pyenchant' 'python2-pyenchant' 'python-sphinx'
             'python2-sphinx' 'python-gobject' 'python2-gobject')
source=(https://pypi.org/packages/source/p/pygtkspellcheck/pygtkspellcheck-$pkgver.tar.gz)
sha256sums=('af90465c11cd0362a802d19d60166d5e22ab58c0e610e731d29f6ffcf97c372b')

package_python-gtkspellcheck() {
  depends=('python-pyenchant' 'python-gobject')

  cd "$srcdir/pygtkspellcheck-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1

  # Fix overly restricted locale permissions
  chmod 644 "$pkgdir"/usr/share/locale/*/LC_MESSAGES/py3gtkspellcheck.mo
}

package_python2-gtkspellcheck() {
  depends=('python2-pyenchant' 'python2-gobject')

  cd "$srcdir/pygtkspellcheck-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1

  # Fix overly restricted locale permissions
  chmod 644 "$pkgdir"/usr/share/locale/*/LC_MESSAGES/py2gtkspellcheck.mo
}

# vim:set ts=2 sw=2 et:
