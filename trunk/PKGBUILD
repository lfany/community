# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=parity
pkgver=1.6.3
_commit=ccc57328e229eccd351e692a4b64662b9d88a516
pkgrel=1
pkgdesc='Fast, light, robust Ethereum implementation'
arch=('i686' 'x86_64')
url='https://ethcore.io/parity.html'
license=('GPL3')
depends=('libsystemd' 'openssl')
makedepends=('cargo' 'git' 'systemd')
source=("git+https://github.com/ethcore/parity.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
    cd ${pkgname}

    cargo build --release
}

package() {
    cd ${pkgname}

    install -Dm755 -t "${pkgdir}"/usr/bin target/release/parity
    install -Dm644 -t "${pkgdir}"/usr/lib/systemd/system scripts/parity.service
}
