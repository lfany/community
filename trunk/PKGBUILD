# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent Bernardoff <vb@luminar.eu.org>

pkgname=aspcud
pkgver=1.9.1
pkgrel=1
pkgdesc='Solver for package dependencies'
arch=('x86_64' 'i686')
url='http://potassco.sourceforge.net/'
license=('GPL3')
depends=('clasp' 'clingo')
makedepends=('boost' 'cmake' 're2c' 'ninja')
source=("http://downloads.sourceforge.net/project/potassco/$pkgname/$pkgver/$pkgname-$pkgver-source.tar.gz")
sha256sums=('e0e917a9a6c5ff080a411ff25d1174e0d4118bb6759c3fe976e2e3cca15e5827')

build() {
  cd "$pkgname-$pkgver-source"

  mkdir -p build/release
  cd build/release
  cmake \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCUDF2LP_LOC=cudf2lp \
    -DGRINGO_LOC=gringo \
    -DCLASP_LOC=clasp \
    -DCMAKE_BUILD_TYPE=Release ../.. \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C "$pkgname-$pkgver-source/build/release" install
}

# vim:set ts=2 sw=2 et:
