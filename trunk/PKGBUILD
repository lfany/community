# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Danial Olsen <do@meyl.fo>

pkgname=kvpnc
pkgver=0.9.6a
pkgrel=8
pkgdesc="A KDE Desktop Environment frontend for various vpn clients"
arch=(i686 x86_64)
url="http://home.gna.org/kvpnc/"
license=("GPL")
depends=('kdelibs' 'hicolor-icon-theme' 'net-tools')
makedepends=('cmake' 'automoc4' 'optipng')
install=kvpnc.install
validpgpkeys=('9944BAA2BAF06D79131CDEEF77D2514C3E528D18')
source=(http://download.gna.org/$pkgname/$pkgname-${pkgver}-kde4.tar.bz2{,.sig}
        "vpnc-paths.patch")
sha256sums=('6bf8c1f13f8d54f73f7ebb4102f912a5ebc01697ea36975977de8e57c97771f3'
            'SKIP'
            'c1444391aa894819a7a33d9d3dfb4939188e92cf53949be741940b86fadb36c7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver-kde4"

  patch -Np0 <"$srcdir/vpnc-paths.patch"

  sed -i '1,1i#include <unistd.h>' src/kvpnc.cpp
}

build() {
  cd "$srcdir"

  export LDFLAGS=-lX11
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver}-kde4 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}
