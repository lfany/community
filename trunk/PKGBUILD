# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: Lucy <lucy@luz.lu>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=1.2.1
pkgrel=1

pkgdesc='Light pkg-config replacement'
url='https://github.com/pkgconf/pkgconf'
arch=('i686' 'x86_64')
license=('custom:ISC')

conflicts=('pkg-config')
provides=('pkg-config')

source=("https://distfiles.dereferenced.org/pkgconf/pkgconf-$pkgver.tar.xz")

sha256sums=('62d3aee56f649f0fd755f472485970d97a4202e5373bd780d632457708805233')

build() {
	cd pkgconf-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	cd pkgconf-$pkgver
	make DESTDIR="$pkgdir" install
	ln -s /usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
