# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>
pkgname=('python-cherrypy' 'python2-cherrypy')
pkgver=8.8.0
pkgrel=1
pkgdesc="A pythonic, object-oriented web development framework"
arch=('any')
url="http://www.cherrypy.org"
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("https://pypi.python.org/packages/1c/db/af94992bb0fb74f5968248e9e3ec5f405703c97cd97578874c9fb08a3bfa/CherryPy-8.8.0.tar.gz")
md5sums=('71bb74990ff2016826a4fe762d0c07a2')

build() {
  cp -r CherryPy-${pkgver} CherryPy-${pkgver}-py2

  cd CherryPy-${pkgver}
  python ./setup.py build

  cd "${srcdir}/CherryPy-${pkgver}-py2"
  python2 ./setup.py build
}

package_python-cherrypy() {
  depends=('python' 'python-six')

  cd CherryPy-${pkgver}

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/python-cherrypy/LICENSE.md"
}

package_python2-cherrypy() {
  depends=('python2')

  cd CherryPy-${pkgver}-py2

  python2 ./setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/cherryd" "${pkgdir}/usr/bin/cherryd2"

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/python2-cherrypy/LICENSE.md"
}

check() {
  # backports.unittest_mock is not packaged..
  cd CherryPy-${pkgver}-py2/cherrypy
  #LANG="en_US.UTF-8" pytest2 test/

  cd $srcdir/CherryPy-${pkgver}/cherrypy
  LANG="en_US.UTF-8" pytest test/

}
