# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Frane Bandov <frane@offbyte.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=log4net
pkgver=1.2.15
pkgrel=1
pkgdesc="A tool to help the programmer output log statements to a variety of output targets"
arch=('any')
url="http://logging.apache.org/log4net/"
license=('Apache')
depends=('mono')
source=(https://www.apache.org/dist/logging/$pkgname/source/$pkgname-$pkgver-src.zip{,.asc}
        $pkgname.pc)
validpgpkeys=('CE8075A251547BEE249BC151A2115AE15F6B8B72') # Stefan Bodewig
md5sums=('f87a8553222a70a58cf51d9f8d6c5947'
         'SKIP'
         'a292da72afbd6bd705e02f4ddb564fd7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s/@VERSION@/$pkgver/" "$srcdir/$pkgname.pc"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir bin
  dmcs -t:library -out:bin/$pkgname.dll -keyfile:$pkgname.snk -d:NET_2_0 \
       -r:System,System.Data,System.Xml,System.Web,System.Net,System.Configuration \
       $(find src -name "*.cs")
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  gacutil -i bin/$pkgname.dll -package $pkgname-$pkgver -root "$pkgdir/usr/lib/"
  install -Dm644 "$srcdir/$pkgname.pc" "$pkgdir/usr/lib/pkgconfig/$pkgname.pc"
}
