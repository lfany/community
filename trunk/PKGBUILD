# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-tool-kit
pkgver=0.2.1
_commit=20e36728822c43fb9a2abb1012770426c9e13959
pkgrel=3
pkgdesc='Base development tool of all C++/Qt Developer work on Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-tool-kit"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools' 'git')
replaces=('libdui')
source=("git+https://github.com/linuxdeepin/deepin-tool-kit.git#commit=$_commit"
        qt-5.8.patch)
sha256sums=('SKIP'
            '7eb88e018bd0fb48fb51f36fc97b203792770c7a2593a0a885e6614cb126ba05')

prepare() {
  cd deepin-tool-kit
  patch -p1 -i ../qt-5.8.patch
}

build() {
  cd deepin-tool-kit
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-tool-kit
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
