# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-mupen64plus
pkgver=4427
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=('x86_64')
url='https://github.com/libretro/mupen64plus-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl')
makedepends=('git' 'mesa')
_commit='78f37eca5033b1c931e8e10b3b643f12c6c89273'
source=("libretro-mupen64plus::git+https://github.com/libretro/mupen64plus-libretro.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mupen64plus_libretro.info')
sha256sums=('SKIP'
            'a1d6b1190a3024b41bc8eb0b590bcdd651fd9cc3deb42c535d52ce3ab356cfee')

pkgver() {
  cd libretro-mupen64plus

  git rev-list --count HEAD
}

build() {
  cd libretro-mupen64plus

  make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-mupen64plus

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mupen64plus_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mupen64plus_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
