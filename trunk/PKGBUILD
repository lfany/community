# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=parity
pkgver=1.4.5
pkgrel=2
pkgdesc='Fast, light, robust Ethereum implementation'
arch=('i686' 'x86_64')
url='https://ethcore.io/parity.html'
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ethcore/parity/archive/v${pkgver}.tar.gz")
sha256sums=('7ea89e46ee79ac95b7be48070235527532bbc894be83d5451df7f5568916719e')

build() {
    cd ${pkgname}-${pkgver}
    
    cargo build --release
}

package() {
    cd ${pkgname}-${pkgver}

    install -Dm755 -t "${pkgdir}"/usr/bin target/release/parity
    install -Dm644 -t "${pkgdir}"/usr/lib/systemd/system scripts/parity.service
}
