# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Samuel Mathieson <smathieson@gmail.com>

pkgname=moinmoin
_xpkgname=moin
pkgver=1.9.9
pkgrel=1
pkgdesc="A python wiki clone"
arch=(any)
url="http://moinmo.in/"
license=('GPL')
depends=('python2')
conflicts=('moin')
install=moinmoin.install
validpgpkeys=('6D5BEF9ADD2075805747B70F9F88FB52FAF7B393')
source=(https://static.moinmo.in/files/moin-$pkgver.tar.gz{,.asc})
sha256sums=('4397d7760b7ae324d7914ffeb1a9eeb15e09933b61468072acd3c3870351efa4'
            'SKIP')

build() {
  cd "$srcdir"/${_xpkgname}-$pkgver

  # python2 fix
  for file in $(find . -name '*.py' -print) wiki/server/*; do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py build
}

package() {
  cd "$srcdir"/${_xpkgname}-$pkgver
  python2 setup.py install --root="$pkgdir" --record=INSTALLED_FILES
}
