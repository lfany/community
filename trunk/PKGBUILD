# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Fleischer

pkgname=libgap
pkgver=4.8.6
pkgrel=1
pkgdesc="A C library version of the GAP kernel"
arch=(i686 x86_64)
url="https://bitbucket.org/vbraun/libgap"
license=(GPL)
depends=(gap)
makedepends=(git python2-ply)
#source=("https://bitbucket.org/vbraun/$pkgname/downloads/$pkgname-$pkgver.tar.gz")
source=("git+https://bitbucket.org/vbraun/libgap/src#commit=6f4bb7e"
        libgap-4.8.6.patch)
md5sums=('SKIP'
         '11c0a2657c77af80ae41b38c2bb0ba50')

prepare() {
  cd src
  patch -p1 -i ../libgap-4.8.6.patch
  python2 scripts/libGAPify.py --modify src
  autoreconf -vi
}

build() {
  cd src

  ./configure --prefix=/usr
  make
}

package() {
  cd src
  make install DESTDIR="$pkgdir"
}
