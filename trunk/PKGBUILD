# Maintainer:  Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>
pkgname=dub
pkgver=1.2.2
pkgrel=1
pkgdesc="Developer package manager for D programming language"
arch=('i686' 'x86_64')
url="https://github.com/D-Programming-Language/dub"
license=('MIT')
makedepends=('dmd' 'libphobos-devel')
depends=('curl')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dlang/dub/archive/v${pkgver}.tar.gz)
sha512sums=('ee1b54cfaec66eb045573f4892b2a5d87d52642a0d07208295f49f623f149b54d34d4df40eaf0e722b5cb8cfc224b409e3731fc8fac62542737529ee0ea8a4c6')

build()
{
  DC=dmd
  cd ${pkgname}-${pkgver}

  echo Generating version file...
  GITVER=${pkgver}
  echo "module dub.version_;" > source/dub/version_.d
  echo "enum dubVersion = \"$GITVER\";" >> source/dub/version_.d
  echo "enum initialCompilerBinary = \"$DC\";" >> source/dub/version_.d

  $DC -ofbin/dub -w -g -version=DubUseCurl -Isource -L-lcurl @build-files.txt
}

package()
{
  cd ${pkgname}-${pkgver}
  install -D -m755 bin/dub "${pkgdir}/usr/bin/dub"
  install -D -m644 scripts/bash-completion/dub.bash $pkgdir/usr/share/bash-completion/completions/dub
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
