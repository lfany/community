# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Henrique C. Alves <hcarvalhoalves@gmail.com>

pkgname=matchbox-panel
pkgver=2.0
pkgrel=1
pkgdesc="A flexible always present 'window bar' for holding application launchers and small 'applet' style applications"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatchbox' 'startup-notification' 'libpng' 'gtk2' 'dbus-glib')
url="http://matchbox-project.org/"
source=(https://downloads.yoctoproject.org/releases/matchbox/matchbox-panel/2.0/matchbox-panel-$pkgver.tar.bz2)
sha256sums=('77e6cfb336cba09156f394867ac05209f73fabba05129152eca3b4d135c746d8')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  find -type f -name 'Makefile*' -exec sed -i 's|-Werror||g' {} \;
  sed -i 's|sync |xsync |g' applets/showdesktop/showdesktop.c
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  export LDFLAGS=-lX11
  ./configure --sysconfdir=/etc --prefix=/usr \
    --enable-startup-notification
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
