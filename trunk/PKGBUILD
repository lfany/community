# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=lash
pkgver=0.6.0~rc2
_relver=0.6.0.594
pkgrel=10
pkgdesc="A session management system for JACK and ALSA"
arch=('i686' 'x86_64')
#url="http://lash-audio.org"
url="http://lash.nongnu.org/"
license=('GPL')
depends=('dbus' 'gtk2' 'jack' 'libxml2' 'python2')
install=$pkgname.install
source=("https://download.savannah.gnu.org/releases/lash/${pkgname}-${pkgver}.tar.bz2"{,.sig}
	"makefile.patch")
sha256sums=('3c1eb2bd88e1a0722dea5c24be4ede1d80848c963d41751973fae73c47dfe18b'
            'SKIP'
            '38c2cf9b1d4bd9dfd390b69228d1bb1a08649157c9aac6f4a7aad9fb6d1e4c54')

prepare() {
  cd "${srcdir}/${pkgname}-${_relver}"
  patch -p0 < "${srcdir}/makefile.patch"
  # Python2 fixes
  export PYTHON="python2"
  sed -i "s#env python#&2#" clients/lash_control
}

build() {
  cd "${srcdir}/${pkgname}-${_relver}"
  export LDFLAGS="-ldl -lm"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_relver}"
  make DESTDIR="$pkgdir" install
}
