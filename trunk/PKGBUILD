# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Henrique C. Alves <hcarvalhoalves@gmail.com>

pkgname=libmatchbox
pkgver=1.11
pkgrel=1
pkgdesc="Base library for Matchbox WM"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pango' 'libpng' 'libjpeg>=7' 'xsettings-client' 'libxext')
url="http://matchbox-project.org/"
source=(https://downloads.yoctoproject.org/releases/matchbox/libmatchbox/$pkgver/libmatchbox-$pkgver.tar.bz2)
sha256sums=('254cab52e304a3512c8df4be59d690cf3921bbb68a28ede7fe26b93534217b53')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LDFLAGS="-lX11" ./configure --prefix=/usr --enable-pango --enable-jpeg --enable-xsettings
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
