# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=libretro-shaders
pkgname=('libretro-shaders-cg' 'libretro-shaders-glsl' 'libretro-shaders-slang')
pkgver=661
pkgrel=2
pkgdesc='Collection of shaders for libretro'
arch=('any')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
makedepends=('git' 'nvidia-cg-toolkit' 'python' 'retroarch')
provides=('libretro-shaders')
conflicts=('libretro-shaders')
_commit_common='c50557b0895b3538a3e2cebc8c0b166b1428bce4'
_commit_slang='5580b7a821dfae38da6ca610b54cf5da3000ddff'
source=("libretro-shaders-cg::git+https://github.com/libretro/common-shaders.git#commit=${_commit_common}"
        "libretro-shaders-slang::git+https://github.com/libretro/slang-shaders.git#commit=${_commit_slang}")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd libretro-shaders-cg

  git rev-list --count HEAD
}

build() {
  retroarch-cg2glsl libretro-shaders-cg libretro-shaders-glsl
}

package_libretro-shaders-cg() {
  install -dm 755 "${pkgdir}"/usr/share/libretro
  cp -dr --no-preserve='ownership' libretro-shaders-cg "${pkgdir}"/usr/share/libretro/shaders
  rm -rf "${pkgdir}"/usr/share/libretro/shaders/.git
}

package_libretro-shaders-glsl() {
  install -dm 755 "${pkgdir}"/usr/share/libretro
  cp -dr --no-preserve='ownership' libretro-shaders-glsl "${pkgdir}"/usr/share/libretro/shaders
}

package_libretro-shaders-slang() {
  install -dm 755 "${pkgdir}"/usr/share/libretro
  cp -dr --no-preserve='ownership' libretro-shaders-slang "${pkgdir}"/usr/share/libretro/shaders
  rm -rf "${pkgdir}"/usr/share/libretro/shaders/.git
}

# vim: ts=2 sw=2 et:
