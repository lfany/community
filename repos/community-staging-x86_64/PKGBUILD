# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libgap
pkgver=4.8.5
_gapver=4r8p5_2016_09_25-11_49
pkgrel=2
pkgdesc="A C library version of the GAP kernel"
arch=(i686 x86_64)
url="https://bitbucket.org/vbraun/libgap"
license=(GPL)
depends=(gap)
makedepends=(git python2-ply)
#source=("https://bitbucket.org/vbraun/$pkgname/downloads/$pkgname-$pkgver.tar.gz")
source=("git+https://bitbucket.org/vbraun/libgap/src#commit=6f4bb7e"
        "http://www.gap-system.org/pub/gap/gap48/tar.bz2/gap$_gapver.tar.bz2")
md5sums=('SKIP'
         '9aa945cda20191304a369baf2b37cab8')

prepare() {
  cp -r gap4r8/src src
  cd src    
  python2 scripts/libGAPify.py --modify src
  sed -e "s|\[[0-9]*\.[0-9]*\.[0-9]*\]|\[$pkgver\]|g" -i configure.ac
  sed -e "s|SyKernelVersion = \"[0-9]*\.[0-9]*\.[0-9]*\"|SyKernelVersion = \"$pkgver\"|g" -i src/system.c
  autoreconf -vi
}

build() {
  cd src

  ./configure --prefix=/usr
  make
}

package() {
  cd src
  make install DESTDIR="$pkgdir"
}
