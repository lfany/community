# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=kdeconnect
pkgver=1.0.1
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
url='https://community.kde.org/KDEConnect'
license=(GPL)
arch=(i686 x86_64)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca-qt5)
makedepends=(extra-cmake-modules python kdoctools)
optdepends=('sshfs: remote filesystem browser' 'kde-cli-tools: configuration UI')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-kde-$pkgver.tar.xz")
md5sums=('9f2af720aae76bb8a32f29c0fcee0ac7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
