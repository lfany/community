# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=parity
pkgver=1.5.0
pkgrel=1
pkgdesc='Fast, light, robust Ethereum implementation'
arch=('i686' 'x86_64')
url='https://ethcore.io/parity.html'
license=('GPL3')
depends=('openssl')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ethcore/parity/archive/v${pkgver}.tar.gz"
        '35666f718b5097a959c8b9d8ec4bd72915bb223e.patch')
sha256sums=('2999918f6b3f46b87acb3c5d0f362e4964f1079653ab9fea81990c9a601a5d04'
            '66094d441f7f2cd356ac4a5f8ffecbb4d3eb2d5a752aa4fcdcb817489084cb81')

prepare() {
    cd ${pkgname}-${pkgver}

    patch -Np1 -i ../35666f718b5097a959c8b9d8ec4bd72915bb223e.patch
}

build() {
    cd ${pkgname}-${pkgver}

    cargo build --release
}

package() {
    cd ${pkgname}-${pkgver}

    install -Dm755 -t "${pkgdir}"/usr/bin target/release/parity
    install -Dm644 -t "${pkgdir}"/usr/lib/systemd/system scripts/parity.service
}
