# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=geth
pkgver=1.5.0
pkgrel=1
pkgdesc='Official golang implementation of the Ethereum protocol'
arch=('i686' 'x86_64')
url='http://geth.ethereum.org/'
license=('GPL3')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ethereum/go-ethereum/archive/v${pkgver}.tar.gz")
sha256sums=('0005782b9c43cca81f2455fad560e6e7db57bda35a9f76312adcaa4cd6a28356')

build() {
    cd go-ethereum-${pkgver}
    
    make all
}

package() {
    cd go-ethereum-${pkgver}
    
    install -dm755 "${pkgdir}"/usr/bin
    install -m755 build/bin/* "${pkgdir}"/usr/bin
}
