# $Id$
# Maintainer: Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>
# Contributor: Jerome Berger <jeberger@free.fr>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=('gdc' 'libgphobos-devel' 'libgphobos')
pkgver=6.2.1
pkgrel=1
_islver=0.16.1
_gcc_commit=c2103c1
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/D-Programming-GDC/GDC"
makedepends=('binutils>=2.26' 'git')

source=(
    git://gcc.gnu.org/git/gcc.git#commit=$_gcc_commit
    http://isl.gforge.inria.fr/isl-$_islver.tar.bz2
    gdc::git://github.com/D-Programming-GDC/GDC.git#tag=v2.068.2_gcc6
    git+https://github.com/D-Programming-GDC/GDMD.git
    paths.diff
)
sha256sums=(
    'SKIP'
    '412538bb65c799ac98e17e8cfcdacbb257a57362acfaaff254b0fcae970126d2'
    'SKIP'
    'SKIP'
    'e04cea391007ebab5d9b0ff33179c1fded0afcf43059b811290472ea52222361'
)

_libdir="usr/lib/gcc/$CHOST/$pkgver"

prepare() {
    cd $srcdir/gcc

    # link isl for in-tree build
    ln -s ../isl-$_islver isl

    # Do not run fixincludes
    sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

    # Arch Linux installs x86_64 libraries /lib
    [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    # GDC setup
    cd $srcdir/gdc
    git apply $srcdir/paths.diff
    ./setup-gcc.sh ../gcc

    mkdir $srcdir/gcc-build
}

build() {
    cd $srcdir/gcc-build

    # using -pipe causes spurious test-suite failures
    # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
    CFLAGS=${CFLAGS/-pipe/}
    CXXFLAGS=${CXXFLAGS/-pipe/}

    $srcdir/gcc/configure --prefix=/usr \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
        --mandir=/usr/share/man \
        --infodir=/usr/share/info \
        --with-bugurl=https://bugs.archlinux.org/ \
        --enable-shared \
        --enable-static \
        --enable-threads=posix \
        --enable-libmpx \
        --with-system-zlib \
        --with-isl \
        --enable-__cxa_atexit \
        --disable-libunwind-exceptions \
        --enable-clocale=gnu \
        --disable-libstdcxx-pch \
        --disable-libssp \
        --enable-gnu-unique-object \
        --enable-linker-build-id \
        --enable-lto \
        --enable-plugin \
        --enable-install-libiberty \
        --with-linker-hash-style=gnu \
        --enable-gnu-indirect-function \
        --enable-default-pie \
        --disable-multilib \
        --disable-werror \
        --enable-languages=d \
        gdc_include_dir=/usr/include/dlang/gdc

   make -j 8
}

package_gdc()
{
    depends=('gcc' 'perl' 'binutils' 'libgphobos-devel')
    provides=("d-compiler")
    pkgdesc="Compiler for D programming language which uses gcc backend"

    # compiler
    install -D -m755 $srcdir/gcc-build/gcc/gdc $pkgdir/usr/bin/gdc
    install -D -m755 $srcdir/gcc-build/gcc/cc1d $pkgdir/usr/lib/gcc/$CHOST/$pkgver/cc1d

    # tools
    install -D -m755 $srcdir/GDMD/dmd-script $pkgdir/usr/bin/gdmd
    install -D -m644 $srcdir/GDMD/dmd-script.1 $pkgdir/usr/share/man/man1/gdmd.1
}


package_libgphobos-devel()
{
    pkgdesc="Standard library for D programming language, GDC port"
    provides=("d-runtime" "d-stdlib")
    options=("staticlibs")

    cd $srcdir/gcc-build
    make -C $CHOST/libphobos DESTDIR=$pkgdir install

    # remove shared library bits, they go into own package
    rm $pkgdir/usr/lib/lib*so*
}

package_libgphobos()
{
    pkgdesc="Shared standard library for D programming language, GDC port"
    provides=("d-runtime" "d-stdlib")

    cd $srcdir/gcc-build
    install -D -m644 $CHOST/libphobos/src/.libs/libgphobos.so $pkgdir/usr/lib/libgphobos.so
    install -D -m644 $CHOST/libphobos/libdruntime/.libs/libgdruntime.so $pkgdir/usr/lib/libgdruntime.so
}
