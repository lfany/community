# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=netactview
pkgver=0.6.4
pkgrel=2
pkgdesc="A graphical network connections viewer similar in functionality to netstat"
arch=('i686' 'x86_64')
url="http://netactview.sourceforge.net/index.html"
license=('GPL')
depends=('gconf' 'libglade' 'libgtop')
makedepends=('intltool' 'polkit')
optdepends=('polkit: restart the application as root')
source=("http://downloads.sourceforge.net/netactview/${pkgname}-${pkgver}.tar.bz2"
        'drop-libgnome.patch'
        'org.archlinux.pkexec.netactview.policy')
sha256sums=('0cfe68e90862b10a19ab2cc4977a079887eeda00e4069a819c13ece90843fb4a'
            'e5359685c785427c812fd285b1eda3465e27120c186eae35a926881be1985df9'
            '467aa93ad28849061bfd1ea07698cc0154bef0a5a98e4c384260fc53e03b877c')

prepare() {
  cd "${pkgname}-${pkgver}"

  # Remove libgnome dependency
  patch -Np1 -i ../drop-libgnome.patch

  # Use pkexec instead of gksu
  sed -i 's/\[gksu\]/\[pkexec\]/' configure.ac
  sed -i 's/install gksu/install polkit/' src/mainwindow.c

  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Polkit policy
  install -Dm644 ../org.archlinux.pkexec.netactview.policy \
    "$pkgdir/usr/share/polkit-1/actions/org.archlinux.pkexec.netactview.policy"
}
