# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gecko-mediaplayer
pkgver=1.0.9
pkgrel=3
pkgdesc='Browser plugin that uses gnome-mplayer to play media in a web browser.'
arch=('i686' 'x86_64')
url='https://sites.google.com/site/kdekorte2/gecko-mediaplayer'
license=('GPL')
depends=("gnome-mplayer>=${pkgver}" 'dbus-glib' 'gmtk' 'curl')
makedepends=('xulrunner')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kdekorte/${pkgname}/archive/v${pkgver}.tar.gz"
        "np_loadds.patch")
md5sums=('a33121cb7490fc5e0554be349a9ba99a'
         '224c865f5df059ef7ac1430889d66a1f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://code.google.com/p/gecko-mediaplayer/issues/detail?id=191
  patch -Np1 -i ../np_loadds.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
