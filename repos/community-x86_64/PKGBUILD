# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=kvirc
pkgver=4.2.0
pkgrel=12
pkgdesc="Qt-based IRC client"
url="http://www.kvirc.net"
depends=('python2' 'libxss' 'qt4')
arch=(i686 x86_64)
makedepends=('cmake')
license=('GPL')
source=(ftp://ftp.kvirc.de/pub/kvirc/$pkgver/source/$pkgname-$pkgver.tar.bz2 kvirc-fix-build.patch)
md5sums=('bf7229aaae132933a18b6a35125a2328'
         'dd8a892c17cc34107942fa5d7257f118')

prepare() {
  cd $pkgname-$pkgver
  # Fix build
  patch -p1 -i ../kvirc-fix-build.patch
  # Fix overlinking
  sed -e '/lcrypto/d' -i CMakeLists.txt
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DWANT_KDE4=OFF \
    -DWANT_PERL=ON \
    -DWANT_PHONON=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENSSL_INCLUDE_DIR=/usr/include/openssl-1.0 \
    -DOPENSSL_SSL_LIBRARY=/usr/lib/openssl-1.0/libssl.so \
    -DOPENSSL_CRYPTO_LIBRARY=/usr/lib/openssl-1.0/libcrypto.so
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
