# $Id$
# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: <kang@insecure.ws>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.7.3
pkgrel=1
pkgdesc='Userspace components of the audit framework'
url='https://people.redhat.com/sgrubb/audit'
arch=('i686' 'x86_64')
depends=('krb5' 'libcap-ng')
makedepends=('libldap' 'swig' 'linux-headers' 'python')
license=('GPL')
options=('emptydirs')
backup=(
  etc/libaudit.conf
  etc/audit/audit.rules
  etc/audit/auditd.conf
  etc/audisp/audispd.conf
  etc/audisp/audisp-remote.conf
  etc/audisp/zos-remote.conf
  etc/audisp/plugins.d/af_unix.conf
  etc/audisp/plugins.d/audispd-zos-remote.conf
  etc/audisp/plugins.d/au-remote.conf
  etc/audisp/plugins.d/syslog.conf
)
source=("${pkgname}-${pkgver}.tar.gz::https://people.redhat.com/sgrubb/audit/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0f504169e1a41078a2244d47418b37f714003e637656af53548bb041f48bbab2422c9e25f78fcf48fa7e57aa6a47336f4b37428df7676b13b3df64d20cd1a2f2')
install="audit.install"

build() {
  cd "${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python3
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/audit \
    --with-python=yes \
    --enable-gssapi-krb5=yes \
    --enable-systemd=yes \
    --with-libcap-ng=yes
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  install -d var/log/audit
  rm -rf etc/rc.d etc/sysconfig usr/lib/audit

  sed -ri 's|/sbin|/usr/bin|' \
    etc/audit/*.conf \
    etc/audisp/plugins.d/*.conf \
    usr/lib/systemd/system/auditd.service

  chmod 644 usr/lib/systemd/system/auditd.service
}
