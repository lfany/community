# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=processing
pkgver=3.2.3
_rev=0255
pkgrel=3
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
makedepends=('gendesk' 'java-openjfx' 'java-environment=8' 'apache-ant' 'rsync')
depends=('libgl' 'java-openjfx')
options=('!strip')
source=("https://github.com/$pkgname/$pkgname/archive/$pkgname-$_rev-$pkgver.tar.gz")
sha256sums=('e2959b16375fa05bfaa2a2d02c5ddb88bd64aca4efd8366a831ace0e2bd119b2')

prepare() {
  gendesk -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

build() {
  cd "$pkgname-$pkgname-$_rev-$pkgver/build"

  ant run
}

package() {
  cd "$pkgname-$pkgname-$_rev-$pkgver/build"

  install -d "$pkgdir/usr/bin/" "$pkgdir/usr/share/$pkgname/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"

  # Desktop icon / application shortcut
  install -Dm644 "shared/lib/icons/pde-256.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# getver: https://raw.githubusercontent.com/processing/processing/master/build/shared/revisions.txt
# vim:set ts=2 sw=2 et:
