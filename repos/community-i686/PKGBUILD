# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Morgan LEFIEUX <comete_AT_archlinuxfr.org>

pkgname=python2-ldap
pkgver=2.4.32
pkgrel=1
pkgdesc="Provides an object-oriented API to access LDAP directory servers from Python programs"
arch=('i686' 'x86_64')
url="http://python-ldap.sourceforge.net"
license=('custom')
depends=('python2' 'libldap>=2.4.26')
makedepends=('chrpath')
replaces=('python-ldap')
source=("https://pypi.python.org/packages/67/d9/fa0ea70d1792875745116ad62ac8d4bcb07550b15cded591bb57df6a6d9a/python-ldap-$pkgver.tar.gz")
sha256sums=('5810f1b5a9ae9255df99fb9c2dcab7352fed325687efda56c0faae1a82c5e3cb')

build() {
  cd "$srcdir"/python-ldap-$pkgver
  sed -i 's|<sasl.h>|<sasl/sasl.h>|' Modules/LDAPObject.c
  python2 setup.py build
}

package() {
  cd "$srcdir"/python-ldap-$pkgver
  python2 setup.py install --root "$pkgdir"
  install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chrpath -d "$pkgdir"/usr/lib/python2.7/site-packages/_ldap.so
}
