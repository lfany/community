# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: maz-1 <loveayawaka@gmail.com>

pkgname=ndiswrapper-dkms
pkgver=1.61
pkgrel=1
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors.Use with DKMS"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ndiswrapper/"
license=('GPL')
depends=('dkms')
#optdepends=('ndisgtk: GTK+ based frontend for ndiswrapper.')
provides=('ndiswrapper')
conflicts=('ndiswrapper')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pgiri/ndiswrapper/archive/v$pkgver.tar.gz"
        "dkms.conf")
md5sums=('1267784d65a8ba6a92884cf6f3bffb9a'
         '801e409f0b93a0434e5c923831c49d50')

build() {
  cd ndiswrapper-$pkgver/ndiswrapper
  make -C utils
}

package() {
  cd ndiswrapper-$pkgver/ndiswrapper
  mkdir -p "$pkgdir"/usr/src
  cp -RL ./driver "$pkgdir"/usr/src/ndiswrapper-$pkgver
  cp "$srcdir"/dkms.conf "$pkgdir"/usr/src/ndiswrapper-$pkgver
  make -C utils sbindir=/usr/bin usrsbindir=/usr/bin DESTDIR="$pkgdir" install
}
