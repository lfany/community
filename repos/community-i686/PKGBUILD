# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gir-generator
pkgver=1.0.1
pkgrel=1
pkgdesc='Generate static golang bindings for GObject'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/go-gir-generator"
license=('GPL3')
depends=('gobject-introspection' 'libgudev')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-gir-generator/archive/$pkgver.tar.gz"
        SettingsBackendLike.patch)
sha512sums=('06bf5ad500db09daa3a2427c1a625cf31f01a28c77e82cfd92ac748169b4f6175875e0be2974b5a29104bf26694744ced7e03ae955fbfa458d8481b4d907306e'
            'b36b10c01afb33f7bb054a928d4c5e0a017fac35e38992fe441bfcb9224b3aebd55e77a2f740a7b439221694f103a0edd102bd9bc0ef72a45512514886a90ea8')

prepare() {
  export GOPATH="$srcdir/go-gir-generator/vender:$srcdir/go-gir-generator"

  cd go-gir-generator-$pkgver
  # Our gobject-introspection is too new
  # https://cr.deepin.io/#/c/16880/
  patch -p1 -i ../SettingsBackendLike.patch
}

build() {
  cd go-gir-generator-$pkgver
  make
}

package() {
  cd go-gir-generator-$pkgver
  make DESTDIR="$pkgdir" install
}
