# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker at gmail dot com>

pkgname=pulp
pkgver=11.0.0
pkgrel=1
pkgdesc="A build system for PureScript projects"
arch=('any')
url="https://github.com/bodil/pulp"
license=('LGPL')
depends=('nodejs' 'acorn' 'browserify' 'bower' 'purescript')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('e931308e2634d2f91b0db1193e8eeca4b92f4ca5d11e1bfb9750844710075288a5cdd45da3cdd3fdee45567c299a0aeac226f5f5398ab577068958afd883f36b')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn browserify; do
    rm -r $dep;
    npm link $dep;
  done
}
