# $Id: PKGBUILD 172879 2016-04-30 04:50:53Z fyan $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko1
pkgname=(python-paramiko1 python2-paramiko1)
pkgver=1.16.0
pkgrel=3
pkgdesc="Python module that implements the SSH2 protocol (compatibility version)"
url="https://github.com/paramiko/paramiko/"
license=('LGPL')
arch=('any')
makedepends=('python-ecdsa' 'python2-ecdsa' 'python-crypto' 'python2-crypto' 'git')
source=("git+https://github.com/paramiko/paramiko.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a paramiko{,-py2}

  find paramiko-py2 -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

check() {
  cd "$srcdir/paramiko"
  LANG=en_US.UTF-8 python test.py

  cd "$srcdir/paramiko-py2"
  LANG=en_US.UTF-8 python2 test.py
}

package_python-paramiko1() {
  depends=('python-ecdsa' 'python-setuptools' 'python-cryptography')
  conflicts=('python-paramiko')
  provides=('python-paramiko==1.8.0')

  cd "$srcdir/paramiko"

  python setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py
}

package_python2-paramiko1() {
  depends=('python2-ecdsa' 'python2-setuptools' 'python2-cryptography')
  conflicts=('python2-paramiko')
  provides=('python2-paramiko==1.8.0')

  cd "$srcdir/paramiko-py2"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py
}
