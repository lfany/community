# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: sloppybench <sloppybench@eml.cc>
# Contributor: der_FeniX <derfenix@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
# Contributor: Simon Lipp <aur@simon.lipp.name>
# Contributor: Chris Bolton <http://scr.im/chris-kun>
# Contributor: Sergei Lebedev <superbobry at gmail dot com>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname=coffee-script
pkgver=1.12.3
pkgrel=1
pkgdesc='CoffeeScript is a little language that compiles into JavaScript'
arch=('any')
url='http://coffeescript.org'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha256sums=('4494ca0cb76924604d773df3fde730b3fceed02cd3f32a905550f750360bc418')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
