# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=gourmet
pkgver=0.17.4
pkgrel=5
pkgdesc="A simple but powerful recipe-managing application"
arch=('any')
url="http://thinkle.github.io/gourmet/"
license=('GPL')
depends=('pygtk' 'python2-elib-intl' 'python2-pillow' 'python2-sqlalchemy')
makedepends=('python2-distutils-extra')
optdepends=('ipython2: Python Shell plugin'
            'pyrtf: RTF support'
            'python2-beautifulsoup3: Webpage import plugin'
            'python2-gtkspell: Spell Checking plugin'
            'python2-poppler: Printing and PDF Export plugin'
            'python2-reportlab: Printing and PDF Export plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/thinkle/$pkgname/archive/$pkgver.tar.gz"
        "gourmet-pillow.patch")
md5sums=('937334364abc3093709a604c1d473e9f'
         '4fec382fd362959e7062c92b1d2b9fa9')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Fix for pillow 3
  # https://github.com/thinkle/gourmet/commit/4d88788a
  patch -Np1 -i ../gourmet-pillow.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
