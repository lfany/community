# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgbase=apache-libcloud
pkgname=('python-apache-libcloud' 'python2-apache-libcloud')
pkgver=1.5.0
pkgrel=1
pkgdesc="Apache Libcloud is a standard Python library that abstracts away differences among multiple cloud provider APIs"
arch=('any')
url="https://libcloud.apache.org"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-mock' 'python2-mock' 'python2-paramiko' 'openssh')
source=("https://pypi.io/packages/source/a/apache-libcloud/apache-libcloud-${pkgver}.tar.bz2")
sha256sums=('ea3dd7825e30611e5a018ab18107b33a9029097d64bd8b39a87feae7c2770282')

prepare() {
  cp -a apache-libcloud-$pkgver{,-py2}
}

build() {
  cd "$srcdir/apache-libcloud-$pkgver"
  python setup.py build

  cd "$srcdir/apache-libcloud-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/apache-libcloud-$pkgver"
  cp libcloud/test/secrets.py-dist libcloud/test/secrets.py
  python setup.py test
  rm libcloud/test/secrets.py

  cd "$srcdir/apache-libcloud-$pkgver-py2"
  cp libcloud/test/secrets.py-dist libcloud/test/secrets.py
  python2 setup.py test
  rm libcloud/test/secrets.py
}

package_python-apache-libcloud() {
  depends=('python')

  cd apache-libcloud-$pkgver
  python setup.py install --root="$pkgdir"
}

package_python2-apache-libcloud() {
  depends=('python2')
  replaces=('apache-libcloud')
  conflicts=('apache-libcloud')

  cd apache-libcloud-$pkgver-py2
  python2 setup.py install --root="$pkgdir"
}
